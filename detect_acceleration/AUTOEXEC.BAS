100 '---- INIT ----
110 DIM SD$(1):DEFINT A-Z:KEY OFF
120 SCREEN 1,0
130 LT$="device/leds/trans_ms":TR=0:GOSUB 1700
140 D$="device/accel/x":L$="device/leds/data"
150 SD$(0)=STRING$(2,0):
160 SD$(1)=CHR$(0)+CHR$(&HAF)
170 S=0:GOSUB 1600
180 VS=64
190 CLS
200 '---- Wait start ----
210 LOCATE 1,11:PRINT "TOUCH THE SCREEN TO START"
220 IF PAD(4)=0 THEN 220
230 TIME=0:CLS
240 T=TIME:IF T<60 THEN 240
250 GOSUB 1500:A=R
300 '---- Main loop ----
310 GOSUB 1500
320 V=ABS(A-R)
330 IF V>VS THEN 400
340 PRINT V
350 IF PAD(4) THEN END
360 GOTO 310
400 '---- Detected ----
410 S=1:SS=0:SOUND0,64:SOUND1,0:PRINT "Detected!! ";V
420 FOR I=1 TO 9
430 TIME=0:GOSUB 1600:SOUND8,15*S:SWAP S,SS
440 IF TIME<30 THEN 440
450 NEXTI
460 SOUND8,0
470 IF PAD(4)=0 THEN 470
480 TIME=0
490 IF TIME>120 THEN 900
500 IF PAD(4) THEN 490
510 TIME=0:SOUND8,0:S=0:GOSUB 1600:CLS
520 IF TIME<60 THEN 520
530 GOTO 200
900 '---- END ----
910 SOUND8,0:S=0:GOSUB1600
920 END
1500 '---- IOT GET v0.07.08 later ----
1510 OUT&H58,&HE0:OUT&H58,1:OUT&H58,&H53
1520 OUT&H58,&HC0:L=LEN(D$):OUT&H58,L:FORZ=1TOL:OUT&H58,ASC(MID$(D$,Z,1)):NEXTZ:OUT&H58,0:R=INP(&H58)
1530 OUT&H58,&HE0:OUT&H58,1:OUT&H58,1:OUT&H58,&H80
1540 R=INP(&H58):P=VARPTR(R):POKEP,INP(&H58):POKEP+1,INP(&H58)
1550 RETURN
1600 '---- IOT PUTS v0.07.08 later ----
1610 LD$=SD$(S)
1620 OUT&H58,&HE0:OUT&H58,1:OUT&H58,&H53
1630 OUT&H58,&HC0:L=LEN(L$):OUT&H58,L:FORZ=1TOL:OUT&H58,ASC(MID$(L$,Z,1)):NEXTZ:OUT&H58,0:R=INP(8)
1640 OUT&H58,&HE0:OUT&H58,1:OUT&H58,&H43
1650 OUT&H58,&HC0:L=LEN(LD$):OUT&H58,L:FORZ=1TOL:OUT&H58,ASC(MID$(LD$,Z,1)):NEXTZ:OUT&H58,0:R=INP(8)
1660 RETURN
1700 '---- IOT PUTI v0.07.08 later ----
1710 OUT&H58,&HE0:OUT&H58,1:OUT&H58,&H53
1720 OUT&H58,&HC0:L=LEN(LT$):OUT&H58,L:FORI=1TOL:OUT&H58,ASC(MID$(LT$,I,1)):NEXTI:OUT&H58,0:R=INP(8)
1730 OUT&H58,&HE0:OUT&H58,1:OUT&H58,&H41
1740 P=VARPTR(TR):OUT&H58,&HC0:OUT&H58,2:OUT&H58,PEEK(P):OUT&H58,PEEK(P+1)
1750 RETURN
