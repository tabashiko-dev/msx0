100 '---- INIT ----
110 CLEAR 384:DEFINT A-Z
120 GOSUB 4000
130 DD$="device/vibration"
140 SCREEN 2,2:OPEN "GRP:" FOR OUTPUT AS #1
150 SPRITE$(0)=STRING$(32,CHR$(&HFF))
200 '---- START ----
210 GOSUB 4500
220 COLOR 15,1,1:CLS:GOSUB 3000
230 LINE(7,7)-(248,184),2,B
240 X=32:Y=32:XA=0:YA=0:XD=X*100:YD=Y*100:NG=0:C=2:MX=16:MY=16
250 GOSUB 800:TIME=0
300 '---- MAIN @ TURBO ----
310 _TURBO ON(X,Y,XA,YA,XD,YD,NG,C,MX,MY,TX,TY)
320 DX$="device/accel/x":DY$="device/accel/y":DZ$="device/accel/z"
352 'XA=XA+(XA>0)-(XA<0)
353 'YA=YA+(YA>0)-(YA<0)
354 'S=STICK(0)+STICK(1)
355 'XD=(S>=6 AND S<=8)-(S>=2 AND S<=4)
356 'YD=(S=1 OR S=2 OR S=8)-(S>=4 AND S<=6)
365 GOSUB 450
370 X=XD/100'XA=XA+XD+XD:X=X+XA
380 Y=YD/100'YA=YA+YD+YD:Y=Y+YA
390 PUT SPRITE 10,(X,Y-1),15,0
400 NG=(X<=7)+(X>=233)+(Y<=7)+(Y>=169):IF NG THEN 600 'GAME OVER
410 IF ABS(X-TX)<16 AND ABS(Y-TY)<16 THEN 600 'HIT
420 GOTO 320
450 '---- CHECK ACCEL ----
460 D$=DX$:GOSUB 500:XD=XD+R
470 D$=DY$:GOSUB 500:YD=YD-R
480 RETURN
500 '---- IOT GET v0.07.08 later ----
510 OUT&H58,&HE0:OUT&H58,1:OUT&H58,&H53
520 OUT&H58,&HC0:L=LEN(D$):OUT&H58,L:FORZ=1TOL:OUT&H58,ASC(MID$(D$,Z,1)):NEXTZ:OUT&H58,0:R=INP(&H58)
530 OUT&H58,&HE0:OUT&H58,1:OUT&H58,1:OUT&H58,&H80
540 R=INP(&H58):P=VARPTR(R):POKEP,INP(&H58):POKEP+1,INP(&H58)
550 RETURN
600 _TURBO OFF
700 '---- CHECK STATE ----
720 IF NG THEN GOSUB 1000: GOTO 2000 'GAME OVER
730 WT=5:GOSUB 5500:GOSUB 1500:GOSUB 2270 'NEXT STAGE
740 C=C+1:IF C>15 THEN GT$=TM$:GOTO 900 'GAME CLEAR
750 GOSUB 800:GOTO 300 'START STAGE
800 '---- NEXT TARGET ----
810 TX=RND(1)*216+8:TY=RND(1)*152+8
820 PUT SPRITE 11,(TX,TY-1),C,0
830 RETURN
900 '---- GAME CLEAR ----
910 PUT SPRITE 10,(X,Y-1),15,0
920 LINE (191,152)-(64,144),1,BF:PRINT #1,"CONGRATULATIONS!"
930 GOSUB 1300
940 LINE (191,168)-(64,160),1,BF:PRINT #1,"    "+GT$
950 GOSUB 3400
960 GOSUB 5600 'w/LED effect
980 GOTO 200 'GO BACK TITLE
1000 '---- MODIFY POSITION TO END ----
1010 IF X<=7 THEN X=7:XA=0
1020 IF X>=233 THEN X=233:XA=0
1030 IF Y<=8 THEN Y=7:YA=0
1040 IF Y>=169 THEN Y=169:YA=0
1050 PUT SPRITE 10,(X,Y-1),15,0
1060 RETURN
1200 ' ---- WAIT ----
1210 TIME=0
1220 IF TIME<60 THEN 1220
1230 RETURN
1300 '---- UPDATE TIME ----
1310 T#=TIME:LM=T#/3600:LT=T#-LM*3600#:LS=LT/60:LT=LT-LS*60:LZ=LT*10/6
1320 SA=LM:GOSUB 1400:TM$=SR$+":"
1330 SA=LS:GOSUB 1400:TM$=TM$+SR$+":"
1340 SA=LZ:GOSUB 1400:TM$=TM$+SR$
1350 RETURN
1400 '---- SET NUMBER OF TIME ----
1410 IF SA<10 THEN SR$="0" ELSE SR$=""
1420 SR$=SR$+MID$(STR$(SA),2,2)
1430 RETURN
1500 '---- DRAW TIME ----
1510 GOSUB 1300
1520 IF C=9 THEN MY=16
1530 IF C<9 THEN MX=16 ELSE MX=128
1540 LINE(MX,MY)-STEP(7,7),C,BF
1550 PRESET STEP(9,-7):PRINT #1,TM$
1560 MY=MY+16
1570 RETURN
2000 '---- GAME OVER ----
2010 GOSUB 2500:WT=10:GOSUB 3500
2020 LINE (191,152)-(64,144),1,BF:PRINT #1,"   GAME OVER!   "
2030 GOSUB 5700 'LED effect
2040 GOSUB 1200
2050 GOTO 200 'GO BACK TITLE
2200 ' SE:PI
2210 SOUND 0,64:SOUND 1,0
2220 SOUND 12,16:SOUND 11,0
2230 SOUND 8,16
2240 SOUND 7,&B111110
2250 SOUND 13,1
2260 RETURN
2270 ' SE:PU
2280 SOUND 0,250:SOUND 1,0
2290 SOUND 12,2:SOUND 11,0:GOTO 2220
2500 ' SE:BANG
2510 SOUND 6,31
2520 SOUND 7,&B110111
2530 SOUND 8,16
2540 SOUND 11,0
2550 SOUND 12,31
2560 SOUND 13,1
2570 RETURN
3000 '---- TITLE ----
3010 GOSUB 3400
3020 LINE(40,72)-(215,111),4,BF
3030 PSET(64,88),4:PRINT #1,"  COLOR SQUARE  "
3040 PSET(24,128),0:PRINT #1,"TOUCH THE SCREEN TO START!"
3050 GOSUB 3100
3060 GOSUB 2200:CLS
3070 RETURN
3100 '---- WAIT USER INPUT ----
3110 S=STRIG(0)+STRIG(1)+PAD(4):IF S=0 THEN 3110
3120 RETURN
3400 '---- ERASE SPRITE ----
3410 PUT SPRITE 10,(0,208),0,0
3420 RETURN
3500 '---- Vibration ON/OFF ----
3510 TT=TIME
3520 _IOTPUT(DD$,100):TIME=0
3530 IF TIME<WT THEN 3530
3540 _IOTPUT(DD$,0)
3550 TIME=TT:RETURN
4000 '---- LED Setting ----
4010 DIM LV$(15):GOSUB 4700
4020 _IOTPUT("device/leds/trans_ms",100)
4030 LD$="device/leds/data":GOSUB 4500:
4040 RETURN
4300 '---- LED data update : LX=LED data index : use LD$,LV$ ----
4305 _IOTPUT(LD$,LV$(LX)):RETURN
4307 '---- <NOT USED> LED data update : LX=LED data index : use LD$,LE$,L,R,Z ----
4310 LE$=LV$(LX)
4320 OUT&H58,&HE0:OUT&H58,1:OUT&H58,&H53
4330 OUT&H58,&HC0:L=LEN(LD$):OUT&H58,L:FORZ=1TOL:OUT&H58,ASC(MID$(LD$,Z,1)):NEXTZ:OUT&H58,0:R=INP(8)
4340 OUT&H58,&HE0:OUT&H58,1:OUT&H58,&H43
4350 OUT&H58,&HC0:L=LEN(LE$):OUT&H58,L:FORZ=1TOL:OUT&H58,ASC(MID$(LE$,Z,1)):NEXTZ:OUT&H58,0:R=INP(8)
4360 RETURN
4500 '---- LED OFF ----
4510 LX=0:GOSUB 4300:RETURN
4700 '---- LED DATA ----
4710 LV$(0)=STRING$(2,0) 'OFF
4720 LV$(1)=CHR$(0)+CHR$(&HAF) 'NG
4730 T$=STRING$(8,0)+CHR$(&H71)+CHR$(&H1):LV$(2)=T$
4740 MID$(T$,9,2)=CHR$(&HF0)+CHR$(&H0):LV$(3)=T$
4750 MID$(T$,7,2)=CHR$(&H14)+CHR$(&H1):LV$(4)=T$
4760 MID$(T$,7,2)=CHR$(&HF)+CHR$(&H0):LV$(5)=T$
4770 MID$(T$,5,2)=CHR$(&H11)+CHR$(&H5):LV$(6)=T$
4780 MID$(T$,3,2)=CHR$(&HAF)+CHR$(&H2):LV$(7)=T$
4790 MID$(T$,1,2)=CHR$(&H11)+CHR$(&HF):LV$(8)=T$
4800 LV$(9)=LV$(8)+CHR$(&H33)+CHR$(&HF)
4810 LV$(10)=LV$(9)+CHR$(&H41)+CHR$(&H4)
4820 LV$(11)=LEFT$(LV$(10),12)+CHR$(&HF0)+CHR$(&HF)
4830 LV$(12)=LV$(11)+CHR$(&H41)+CHR$(&H1)
4840 LV$(13)=LV$(12)+CHR$(&H25)+CHR$(&HA)
4850 LV$(14)=LV$(13)+CHR$(&H44)+CHR$(&H4)
4860 LV$(15)=LEFT$(LV$(14),18)+CHR$(&HFF)+CHR$(&HF)
4870 RETURN
5500 '---- Vibration ON/OFF with LED ----
5510 TT=TIME:TIME=0
5515 LX=C:GOSUB 4300
5520 _IOTPUT(DD$,100):TIME=0
5530 IF TIME<WT THEN 5530
5540 _IOTPUT(DD$,0)
5550 TIME=TT:RETURN
5600 '---- WAIT USER INPUT w/OK effect ----
5610 L1=0:L2=15:WT=30:GOSUB 5800
5620 GOSUB3100:GOSUB 4500:RETURN
5700 '---- WAIT USER INPUT w/NG effect ----
5710 IF C=2 THEN L2=0 ELSE L2=C-1
5720 L1=1:WT=30:GOSUB 5800
5730 RETURN
5800 '---- LED Blink L1/L2 ----
5820 FOR I=1 TO 10
5830 LX=L1:GOSUB 4300
5840 TIME=0
5850 S=STRIG(0)+STRIG(1)+PAD(4):IF S<>0 THEN RETURN
5860 IF TIME<WT THEN 5850
5870 SWAP L1,L2
5880 NEXTI
5890 RETURN
